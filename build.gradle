import com.github.blindpirate.gogradle.TestTop1000Task

/************** plugins **************/
plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    id 'findbugs'
    id 'checkstyle'
    id 'jacoco'
    id 'antlr'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id "com.gradle.plugin-publish" version "0.9.7"
}

apply from: 'config/scripts/coverage.gradle'

group 'com.github.blindpirate'
version '0.1.2'

/************** sourcesets **************/
sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    main {
        java {
            srcDirs += ['src/main/antlr']
        }
    }
    integrationTest {
        java.srcDir 'src/integration-test/java'
        groovy.srcDir 'src/integration-test/groovy'
        resources.srcDir 'src/integration-test/resources'
    }
}

/************** dependencies **************/
repositories {
    mavenLocal()
    jcenter()
    maven {
        url "https://repo.eclipse.org/content/groups/jgit/"
    }
}
dependencies {
    integrationTestCompile sourceSets.main.output
    integrationTestCompile sourceSets.test.output
    integrationTestCompile configurations.testCompile
    integrationTestRuntime configurations.testRuntime

    antlr "org.antlr:antlr4:4.6"

    compile gradleApi()
    compile "org.antlr:antlr4:4.6"
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.google.guava:guava:20.0'
    compile 'com.github.zafarkhaja:java-semver:0.9.0'
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.7.0-SNAPSHOT'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.7.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.5'
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.8.5'
//    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.13'
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '1.3.2'
    compile group: 'org.apache.ant', name: 'ant', version: '1.10.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.3.7'
}
/************** test **************/
// Define integration test task
task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

project.tasks.withType(Test) {
    if (gradle.startParameter.isOffline()) {
        it.systemProperty("TEST_ARE_OFFLINE", 'true')
    }
    it.systemProperty("GRADLE_DIST_HOME", gradle.gradleHomeDir)
}

check.dependsOn javadoc
check.dependsOn integrationTest
integrationTest.dependsOn test
//test.dependsOn 'jar'

/************** testTop1000 **********/

task testOne(type: TestTop1000Task) {
    doLast {
        testOne("${testProjectDir}")
    }
}
task testAll(type: TestTop1000Task) {
    doLast {
        testAll("${allTestProjectsDir}")
    }
}

/************** checkstyle **************/

checkstyle {
    toolVersion = '6.7'
}

checkstyleTest {
    enabled = false
}

/************** findbugs **************/

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
    def antlrDirPath = 'com.github.blindpirate.gogradle.antlr'.replace('.', File.separator)
    // FindBugs can't recognize groovy closure
    classes = classes.filter {
        // '\' separator on
        !it.path.contains(antlrDirPath) && !(it.name ==~ /.*_closure\d+\.class/)
    }
}

/************** coverage **************/

jacoco {
    toolVersion = "0.7.7.201606060606"
}
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude:
                            ['com/github/blindpirate/gogradle/antlr/**',
                             'com/github/blindpirate/gogradle/core/exceptions/**',
                             'com/github/blindpirate/gogradle/vcs/bazaar/**',
                             'com/github/blindpirate/gogradle/vcs/svn/**',
                             'com/github/blindpirate/gogradle/vcs/mercurial/**',
                             'com/github/blindpirate/gogradle/util/HttpUtils*',
                             'com/github/blindpirate/gogradle/ProjectDecorator*'])
        })
    }
}

jacocoTestReport.dependsOn 'test', 'integrationTest'
//check.dependsOn 'coverageCheck'

/************** antlr **************/
generateGrammarSource {
    // WARNING: DO NOT put source files here, every time this task executed, this directory will be overwritten
    // Don't ask me how I know it.  T^T
    outputDirectory = file('src/main/antlr/com/github/blindpirate/gogradle/antlr')
    arguments += ['-package', 'com.github.blindpirate.gogradle.antlr']
}

/************** publish **************/
def updateVersion(String file, String oldVersionRegex, String newVersion) {
    String content = new File(rootDir, file).getText()
    content = content.replaceAll(oldVersionRegex, newVersion)
    new File(rootDir, file).write(content)
}

task('updateVersion') {
    doLast {
        updateVersion('README.md', /id 'com.github.blindpirate.gogradle' version '(\w|\d|\.|\-)*'/, "id 'com.github.blindpirate.gogradle' version '${version}'")
        updateVersion('README_CN.md', /id 'com.github.blindpirate.gogradle' version '(\w|\d|\.|\-)*'/, "id 'com.github.blindpirate.gogradle' version '${version}'")
        updateVersion('src/main/java/com/github/blindpirate/gogradle/GogradleGlobal.java', /GOGRADLE_VERSION = "(\w|\d|\.|\-)*"/, "GOGRADLE_VERSION = \"${version}\"")
    }
}

pluginBundle {
    website = 'https://github.com/blindpirate/gogradle'
    vcsUrl = 'https://github.com/blindpirate/gogradle'
    description = 'Full-featured Build Support for Golang'
    tags = ['go', 'golang', 'build', 'test']
    plugins {
        golangPlugin {
            id = 'com.github.blindpirate.gogradle'
            displayName = 'Gogradle - Full-featured Build Plugin for Golang'
        }
    }
}

publishPlugins.dependsOn 'updateVersion'
/************** wrapper **************/

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

